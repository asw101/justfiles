# Password/token management (1Password + macOS Keychain)
# Set PW_SOURCE to "keychain" or "1password" (default: 1password)

# Overrideable source: "1password" or "keychain"
PW_SOURCE := env("PW_SOURCE", "1password")

# Overrideable default secret name
PW_NAME := env("PW_NAME", "github/260200-token")

# List available recipes
default:
    @just --list

# Show documentation
docs:
    @cat "{{justfile_directory()}}/README.md"

# Get a secret by name (uses PW_SOURCE)
[group('1. Secret')]
secret-get name=PW_NAME:
    #!/usr/bin/env bash
    if [[ "{{PW_SOURCE}}" == "keychain" ]]; then
        security find-generic-password -s "{{name}}" -a "github" -w
    else
        op item get "{{name}}" --vault Private --fields password --reveal
    fi

# Set a secret by name (uses PW_SOURCE)
[group('1. Secret')]
secret-set value name=PW_NAME:
    #!/usr/bin/env bash
    if [[ "{{PW_SOURCE}}" == "keychain" ]]; then
        security add-generic-password -U -s "{{name}}" -a "github" -w "{{value}}"
    else
        op item edit "{{name}}" password="{{value}}" --vault Private
    fi

# --- 1Password CLI ---

# Install 1Password CLI via Homebrew
[group('2. 1Password CLI')]
op-install:
    brew install --cask 1password-cli

# Verify the installation
[group('2. 1Password CLI')]
op-verify:
    op --version

# Sign in to 1Password
[group('2. 1Password CLI')]
op-signin:
    eval $(op signin)

# List vaults to confirm everything works
[group('2. 1Password CLI')]
op-test:
    op vault list

# Update 1Password CLI
[group('2. 1Password CLI')]
op-update:
    brew upgrade --cask 1password-cli


