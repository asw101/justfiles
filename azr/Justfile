# Path to the pw Justfile
pw := justfile_directory() / "../pw/Justfile"

# Default to keychain for azr secrets
export PW_SOURCE := env("PW_SOURCE", "keychain")

# Secret name for the SP credentials blob
secret_name := env("AZR_SECRET_NAME", "260200-azr-token")

# List available recipes
default:
    @just --list

# Create an Azure service principal and store the JSON blob
secret-set name=secret_name:
    #!/usr/bin/env bash
    set -euo pipefail
    echo "Creating service principal '{{name}}'..."
    creds=$(az ad sp create-for-rbac \
      --name "{{name}}" \
      --years 1 \
      --output json)
    echo "Storing credentials as '{{name}}'..."
    just --justfile "{{pw}}" secret-set "$(echo "$creds" | jq -c)" "{{name}}"
    echo "Done. Retrieve with: just secret-get"

# Retrieve the stored SP credentials
secret-get name=secret_name:
    @just --justfile "{{pw}}" secret-get "{{name}}"
